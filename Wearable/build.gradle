buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
}

dependencies {
    implementation 'androidx.wear:wear:1.0.0'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    compileOnly 'com.google.android.wearable:wearable:2.5.0'
    implementation 'com.google.android.support:wearable:2.5.0'
    implementation 'net.e175.klaus:solarpositioning:0.0.9'
    implementation 'androidx.annotation:annotation:1.1.0'
    // The below was only needed since upgrading to Android build tools 3.5.0.
    // Not sure why we need it, only that we error out if we don't. Revisit this...
    // We can however make it only for debug. In release mode it's somehow not needed...
    debugImplementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
    'main',     // main sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process

// https://stackoverflow.com/questions/28498688#35041457
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitDate = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'show', '-s', '--format=%ci', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().split(" ")[0]
}

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"

    defaultConfig {
        // Ref: https://developer.android.com/training/wearables/apps/packaging#specify-a-version-code
        versionCode 280040000
        versionName "0.4"

        resValue "string", "git_hash", "${getGitHash()}"
        resValue "string", "git_date", "${getGitDate()}"
        resValue "string", "version_name", versionName

        minSdkVersion 25 // Wear OS 2.0
        targetSdkVersion 28

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']
    }
}
