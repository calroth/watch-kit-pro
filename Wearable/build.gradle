buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.wear:wear:1.3.0'
    implementation 'com.google.android.gms:play-services-wearable:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.1.0'
    compileOnly 'com.google.android.wearable:wearable:2.9.0'
    implementation 'com.google.android.support:wearable:2.9.0'
    implementation 'net.e175.klaus:solarpositioning:2.0.2'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.core:core-splashscreen:1.1.0-alpha02'
}

// https://stackoverflow.com/questions/28498688#35041457
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitDate = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'show', '-s', '--format=%ci', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().split(" ")[0]
}

android {
    namespace 'pro.watchkit.wearable.watchface'

    defaultConfig {
        // Ref: https://developer.android.com/training/wearables/apps/packaging#specify-a-version-code
        versionCode 330901000
        versionName "9.1"

        resValue "string", "git_hash", "${getGitHash()}"
        resValue "string", "git_date", "${getGitDate()}"
        resValue "string", "version_name", versionName

        minSdkVersion 25 // Wear OS 2.0
        //noinspection OldTargetApi
        targetSdkVersion 33
        compileSdk 34

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            java.srcDirs 'src/main/java'
            res.srcDirs 'src/main/res'
        }
    }

    buildFeatures {
        renderScript true
        buildConfig true
    }
}
